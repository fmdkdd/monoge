grammar org.example.domainmodel.Domainmodel with org.eclipse.xtext.common.Terminals

generate domainmodel "http://www.example.org/domainmodel/Domainmodel"

Domainmodel:
  (elements += Element)*;
	
Element:
	Type | PackageDeclaration | Import;

Type:
  DataType | Entity;

PackageDeclaration:
  'package' name=QualifiedName '{'
  (elements += Element)* 
  '}' 
;

Import:
  'import' importedNamespace=QualifiedNameWithWildcard;
  
QualifiedName:
  ID ('.' ID)*;

QualifiedNameWithWildcard:
  QualifiedName '.*'?;
	
DataType:
  'datatype' name=ID;
  
Entity:
  'entity' name=ID ('extends' superType=[Entity|QualifiedName])?
  '{'
  (features += Feature)*
  '}';
  
Feature:
  (many?='many')? name=ID ':' type=[Type|QualifiedName];


