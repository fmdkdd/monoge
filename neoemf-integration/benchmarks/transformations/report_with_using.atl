-- @atlcompiler emftvm

module report_with_using;
create OUT : Table from IN : Viewpoint;

rule Report {
    from s : Viewpoint!Trace
    to   t : Table!Table (
      rows <- s.logs
    )
}

rule Row {
	from s : Viewpoint!Log
	using {
	    component : OclAny = thisModule.getComponent(s.javaClass.package);
	}
	to   t : Table!Row (
	    cells <- Bag{c0, c1, c2, c3} 
	),

	c0 : Table!Cell (
	     content <- s.message
	),
	
	c1 : Table!Cell (
	     content <- s.javaClass.name
	),
	
	c2 : Table!Cell (
	     content <- if component.oclIsUndefined()
	                then 'No component'
	                else component.name
	                endif
	),
	
	c3 : Table!Cell (
	     content <- if component.requirements.isEmpty()
	                then 'No requirements'
	                else component.requirements->collect(r | r.values->first().theValue)
	                       ->iterate(n; res: String = '' | res + '\n' + n)
	     	        endif
	)
}
    
helper def : getComponent(p : OclAny) : OclAny =
    if p.oclIsUndefined()
    then p
    else if p.component.oclIsUndefined()
         then thisModule.getComponent(p.package)
         else p.component
         endif
    endif
;
    